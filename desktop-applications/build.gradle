plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.5'
}

group 'com.therockstarengineer'
version '0.1.0-SNAPSHOT'

apply plugin: 'application'

javafx {
    modules = ['javafx.base', 'javafx.controls', 'javafx.fxml']
    version = "11.0.2"
}

sourceCompatibility = 13

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


mainClassName = "${moduleName}/com.therockstarengineer.desktop.RockstarPassword"

def javaHome = hasProperty('org.gradle.java.home') ? getProperty('org.gradle.java.home') : System.getenv('JAVA_HOME')
def fxJmods = hasProperty('path.to.fx.mods') ? getProperty('path.to.fx.mods') : System.getenv('PATH_TO_FX_MODS')

task jlink(type: Exec) {
    dependsOn 'clean'
    dependsOn 'jar'

    workingDir 'build'

    if (javaHome == null) {
        throw new RuntimeException("java_home is not defined.")
    }

    if (fxJmods == null) {
        throw new RuntimeException("fx_jmods is not defined.")
    }

    commandLine "${javaHome}/bin/jlink", '--module-path', "libs${File.pathSeparatorChar}${fxJmods}",
            '--add-modules', "${moduleName}", '--output', "${moduleName}", '--strip-debug',
            '--compress', '2', '--no-header-files', '--no-man-pages'
}

task start(type: Exec) {
    dependsOn 'jlink'

    workingDir 'build'

    commandLine "${moduleName}/bin/java", '-m', "${mainClassName}"
}
